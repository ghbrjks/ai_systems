% house(Цвет, Национальность, Напиток, Сигареты, Животное)

solve(Houses) :-
    Houses = [house(_,_,_,_,_),
              house(_,_,_,_,_),
              house(_,_,_,_,_),
              house(_,_,_,_,_),
              house(_,_,_,_,_)],

    % 1. Норвежец живёт в первом доме
    nth1(1, Houses, house(_,норвежец,_,_,_)),

    % 2. Англичанин живёт в красном доме
    member(house(красный,англичанин,_,_,_), Houses),

    % 3. Зелёный дом находится слева от белого
    left_of(house(зеленый,_,_,_,_), house(белый,_,_,_,_), Houses),

    % 4. Датчанин пьёт чай
    member(house(_,датчанин,чай,_,_), Houses),

    % 5. Тот, кто курит Marlboro, живёт рядом с тем, кто выращивает кошек
    next_to(house(_,_,_,мальборо,_), house(_,_,_,_,кошки), Houses),

    % 6. Тот, кто живёт в жёлтом доме, курит Dunhill
    member(house(желтый,_,_,данхилл,_), Houses),

    % 7. Немец курит Rothmans
    member(house(_,немец,_,ротманс,_), Houses),

    % 8. Тот, кто живёт в центре, пьёт молоко
    nth1(3, Houses, house(_,_,молоко,_,_)),

    % 9. Сосед того, кто курит Marlboro, пьёт воду
    next_to(house(_,_,_,мальборо,_), house(_,_,вода,_,_), Houses),

    % 10. Тот, кто курит PallMall, выращивает птиц
    member(house(_,_,_,поллмолл,птицы), Houses),

    % 11. Швед выращивает собак
    member(house(_,швед,_,_,собаки), Houses),

    % 12. Норвежец живёт рядом с синим домом
    next_to(house(_,норвежец,_,_,_), house(синий,_,_,_,_), Houses),

    % 13. Тот, кто выращивает лошадей, живёт в синем доме
    member(house(синий,_,_,_,лошади), Houses),

    % 14. Тот, кто курит Winfield, пьет пиво
    member(house(_,_,пиво,винфилд,_), Houses),

    % 15. В зелёном доме пьют кофе
    member(house(зеленый,_,кофе,_,_), Houses),

	member(house(_,_,_,_,рыбки), Houses).

% Сосед слева
left_of(A, B, List) :- append(_, [A,B|_], List).

% Дома рядом
next_to(A, B, List) :- left_of(A, B, List).
next_to(A, B, List) :- left_of(B, A, List).

